//@version=6
indicator("SQZ+MOM - (BUY/SELL) INDICATOR", shorttitle="SQZ Price", overlay=true)

// ---------------------------------------------------------------------------------------------------
// -------------------------------------- **Inputs** -------------------------------------------------
// ---------------------------------------------------------------------------------------------------

lengthBB = input.int(20, title="BB Length")
multBB = input.float(2.0, title="BB MultFactor")
lengthKC = input.int(20, title="KC Length")
multKC = input.float(1.5, title="KC MultFactor")
useTrueRange = input.bool(true, title="Use True Range (KC)")

// ---------------------------------------------------------------------------------------------------
// ---------------------------------------- **Calculations** -----------------------------------------
// ---------------------------------------------------------------------------------------------------

// --------------------------------------- Bandas de Bollinger ---------------------------------------
source = close
basisBB = ta.sma(source, lengthBB)
devBB = multBB * ta.stdev(source, lengthBB)
upperBB = basisBB + devBB
lowerBB = basisBB - devBB

// --------------------------------------- Canales de Keltner ----------------------------------------
basisKC = ta.sma(source, lengthKC)
rangeKC = useTrueRange ? ta.tr : (high - low)
rangemaKC = ta.sma(rangeKC, lengthKC)
upperKC = basisKC + rangemaKC * multKC
lowerKC = basisKC - rangemaKC * multKC

// -------------------------------------- **Squeeze Conditions** -------------------------------------
sqzOn = (lowerBB > lowerKC) and (upperBB < upperKC)
sqzOff = (lowerBB < lowerKC) and (upperBB > upperKC)

// --------------------------------------- Momentum Calculation --------------------------------------
highAvg = ta.highest(high, lengthKC)
lowAvg = ta.lowest(low, lengthKC)
midAvg = (highAvg + lowAvg + ta.sma(close, lengthKC)) / 3
val = ta.linreg(source - midAvg, lengthKC, 0)  // Momentum basado en regresión lineal

// ---------------------------------------------------------------------------------------------------
// ---------------------------------------- **Chart Render** -----------------------------------------
// ---------------------------------------------------------------------------------------------------

// **Histogram Colors**
momentumColorUpper = (val > 0) ? (val > nz(val[1]) ? color.lime : color.new(color.green, 50)) : color.new(color.white, 100)  // Transparente si no es positivo
momentumColorLower = (val < 0) ? (val < nz(val[1]) ? color.red : color.new(color.maroon,20)) : color.new(color.white, 100)  // Transparente si no es negativo

// **Price-Centered Lines**
// Calcula las líneas como desviaciones alrededor del precio
upperHist = close + (val > 0 ? val : 0)  // Extensión positiva del histograma
lowerHist = close - (val < 0 ? math.abs(val) : 0)  // Extensión negativa del histograma

// Plots de las líneas
plot(upperHist, color=momentumColorUpper, style=plot.style_line, linewidth=2, title="Momentum Upper Line")
plot(lowerHist, color=momentumColorLower, style=plot.style_line, linewidth=2, title="Momentum Lower Line")

// Background Highlight
bgcolor(sqzOn ? color.new(color.red, 80) : na, title="Squeeze On Background")
bgcolor(sqzOff ? color.new(color.green, 80) : na, title="Squeeze Off Background")


// ---------------------------------------------------------------------------------------------------
// -------------------------------------- **Signal Logic** -------------------------------------------
// ---------------------------------------------------------------------------------------------------

// Condición para abrir un Short
shortCondition = sqzOff and momentumColorUpper == color.new(color.green, 50)  // Momentum positivo desacelera

// Condición para abrir un Long
longCondition = sqzOff and momentumColorLower == color.new(color.maroon, 20)  // Momentum negativo desacelera

// Señal Visual para Shorts
plotshape(series=shortCondition, title="Sell Signal", location=location.abovebar, color=color.red, style=shape.labeldown, text="SELL", textcolor=color.white)

// Señal Visual para Longs
plotshape(series=longCondition, title="Buy Signal", location=location.belowbar, color=color.green, style=shape.labelup, text="BUY", textcolor=color.white)